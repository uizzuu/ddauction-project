buildscript {
    ext {
        queryDslVersion = "5.0.0"
    }
}

plugins {
    id 'java'
    id 'org.springframework.boot' version '3.5.6'
    id 'io.spring.dependency-management' version '1.1.7'
}

group = 'com.my'
version = '0.0.1-SNAPSHOT'
description = 'Demo project for Spring Boot'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

def querydslDir = "src/main/generated"  // QueryDSL Q클래스 자동생성 디렉토리

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-oauth2-authorization-server'

    // 카카오페이 결제용 HTTP 클라이언트
    implementation 'org.springframework.boot:spring-boot-starter-webflux'
    implementation 'com.squareup.okhttp3:okhttp:4.12.0'

    // WebSocket (실시간 경매)
    implementation 'org.springframework.boot:spring-boot-starter-websocket'

    // JWT 인증
    implementation 'io.jsonwebtoken:jjwt-api:0.12.3'
    runtimeOnly 'io.jsonwebtoken:jjwt-impl:0.12.3'
    runtimeOnly 'io.jsonwebtoken:jjwt-jackson:0.12.3'

    // 메타데이터
    annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'

    // SWAGGER
    implementation("org.springdoc:springdoc-openapi-starter-webmvc-ui:2.8.13")

    // GSON
    implementation group: 'com.google.code.gson', name: 'gson', version: '2.8.9'

    // QUERYDSL
    implementation "com.querydsl:querydsl-jpa:${queryDslVersion}:jakarta"
    implementation "com.querydsl:querydsl-core"
    implementation "com.querydsl:querydsl-collections"
    annotationProcessor "com.querydsl:querydsl-apt:${queryDslVersion}:jakarta"
    annotationProcessor "jakarta.persistence:jakarta.persistence-api" // javax 관련 오류 대응
    annotationProcessor "jakarta.annotation:jakarta.annotation-api" // javax 관련 오류 대응

    compileOnly 'org.projectlombok:lombok'
    runtimeOnly 'com.mysql:mysql-connector-j'
    annotationProcessor 'org.projectlombok:lombok'

    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.security:spring-security-test'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

tasks.named('test') {
    useJUnitPlatform()
}

// Q클래스 생성 폴더 설정 추가
tasks.withType(JavaCompile) {
    options.annotationProcessorGeneratedSourcesDirectory = file(querydslDir)
}

// IDE에서 자동생성 폴더 인식하도록 설정
sourceSets {
    main.java.srcDirs += [ querydslDir ]
}
